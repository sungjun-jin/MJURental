configure(subprojects - project(':libraries:test-common')) {
    publishing {
        repositories {
            maven {
                if (project.version.endsWith('-SNAPSHOT')) {
                    url project.hasProperty('NEXUS_SNAPSHOT_REPOSITORY_URL') ?
                            NEXUS_SNAPSHOT_REPOSITORY_URL : mavenLocal().url
                } else {
                    url project.hasProperty('NEXUS_RELEASE_REPOSITORY_URL') ?
                            NEXUS_RELEASE_REPOSITORY_URL : mavenLocal().url
                }
                credentials {
                    username project.hasProperty('NEXUS_USERNAME') ?
                            NEXUS_USERNAME : ""
                    password project.hasProperty('NEXUS_PASSWORD') ?
                            NEXUS_PASSWORD : ""
                }
                //                    url mavenLocal().url
            }
        }
        android.libraryVariants.all { variant ->
            publications {
                "$variant.name"(MavenPublication) {
                    groupId KAKAO_SDK_GROUP
                    version project.version
                    afterEvaluate {
                        artifactId "${project.name}${variant.artifactIdSuffix}"
                    }
                    artifact sourcesJar
                    artifact(variant.packageLibraryProvider.get().archivePath) {
                        builtBy variant.assembleProvider
                    }
                    pom.withXml {
                        def dependenciesNode = asNode().appendNode('dependencies')
                        //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                        variant.compileConfiguration.allDependencies.each {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            if (it.hasProperty('dependencyProject')) {
                                // kakao sdk modules
                                dependencyNode.appendNode('groupId', it.group)
                                def flavor = android.dependencyFlavors[it.name]
                                List<String> idComponents = [it.name]
                                if (flavor) {
                                    idComponents.add(flavor)
                                }
                                if (variant.buildType.name == 'debug') idComponents.add(variant.buildType.name)
                                String finalArtifactId = idComponents.join('-')
                                dependencyNode.appendNode('artifactId', finalArtifactId)
                                dependencyNode.appendNode('version', project.version)
                                dependencyNode.appendNode('type', 'aar')
                            } else {
                                // external dependencies
                                dependencyNode.appendNode('groupId', it.group)
                                dependencyNode.appendNode('artifactId', it.name)
                                dependencyNode.appendNode('version', it.version)
                            }
                        }

                        def licenses = asNode().appendNode('licenses')
                        def license = licenses.appendNode('license')
                        license.appendNode('name', 'The Apache Software License, Version 2.0')
                        license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                        license.appendNode('distribution', 'repo')
                    }
                }
            }
        }
    }

    ext.configurePublishProperties = { variant ->
        Collection<String> flavors = variant.baseName.split('-')
        flavors.remove('release')
        flavors.remove('full')
        String modifiedFlavor = flavors.join('-')
        variant.ext.artifactIdSuffix = modifiedFlavor == '' ? '' : "-$modifiedFlavor"
    }
}